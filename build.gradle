plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '2.0.2'
    id 'io.freefair.lombok' version '4.1.6'
}

group 'net.notfab'
version '1.0.7'
description 'Virtualizor Java SDK'

sourceCompatibility = targetCompatibility = 8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jar {
    baseName = project.name
    manifest {
        attributes 'Implementation-Version': version
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    repositories {
        maven {
            url = "/repository/Hosted"
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId group
            artifactId rootProject.name
            version version
            artifact javadocJar
            artifact sourcesJar
        }
    }
}

javadoc {
    failOnError = true
    options.memberLevel = JavadocMemberLevel.PUBLIC
    options.author()
    options.encoding = 'UTF-8'
    options.addBooleanOption('html5', true)
}

build {
    dependsOn jar
    dependsOn javadocJar
    dependsOn sourcesJar
    dependsOn shadowJar

    jar.mustRunAfter clean
    javadocJar.mustRunAfter jar
    sourcesJar.mustRunAfter javadocJar
    shadowJar.mustRunAfter sourcesJar
}

repositories {
    mavenCentral()
}

wrapper {
    gradleVersion = '6.3'
}

dependencies {
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.2.2'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.1'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.26'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}
